#### Monday, December 5th 2016

## 006: Introducing awk

The `sed` script `smart_quotes.sed` had a major flaw. It didn't distinguish
between text lines and code lines. So quotes and double quotes have also
been replaced inside code blocks, which messed up their semantics. I didn't
manage to fix the problem using `sed`, so I did what most people would do
in that situation: use `awk` instead.

`markdown` has an easy syntax for code blocks: just indent the line. I do
so by four spaces. So the `awk` script could just ignore indented lines and
print them as they are:

    /^[[:space:]]+/ {
        print;
    }

A line starting with one or more spaces is simply printed out as it is. The
other lines can be captured as follows:

    !/^[[:space:]]+/ {
        # do typographical replacements
    }

This is simply the same pattern, but inverted by using an exclamation
mark. The following replacements must be done: First, straight double and
single quotes must be replaced with their typographically correct counterparts:

    sub(/^"/, "“");
    $0 = gensub(/([[:space:]]+)"/, "\\1“", "g");
    gsub(/"/, "”");

The first line replaces a double quote right after the beginning of the line
with an opening double quote using the `sub()` function. The second line
replaces all double quotes that come after one or many space characters,
also with an opening quote. Those space characters are captured in a group
and retained in the output by using the back reference `\\1`. The global flag
`g` is also passed to make sure that not only one, but all occurrences are
replaced. I had to use the `gensub()` function here, which supports back
references. It doesn't modify the line variable `$0`, but returns the result
of the replacement for manual assignment. All the remaining double quotes
must be replaced by closing double quotes. For this purpose, the `gsub()`
function is called, which works like `sub()` but globally, i.e. handles many
occurrences. (This would be pointless for the first substitution, because
it only replaces the double quote right after the beginning of the line,
and a line begins only once.)

The same is done for single quotes:

    sub(/^'/, "‘");
    $0 = gensub(/([[:space:]]+)'/, "\\1‘", "g");
    gsub(/'/, "’");

Furthermore, three full stops have to be replaced with an ellipsis, and two
hyphens with an n-dash. (I have to consider using m-dashes without surrounding
spaces instead, this page is written in English, after all...) At the end of
the function, the line is printed using the `print` function. If no parameter
is passed, it prints the content of the variable `$0`, which contains the
whole line.

    sub(/\.\.\./, "…");
    sub(/--/, "‒");
    print;

As I've written before, the ordinal numbers in the dates should be displayed
properly; i.e. "December 5<sup>th</sup>" (using superscript) rather than
"December 5th". The following `awk` function handles this:

    /^####/ {
        $0 = gensub(/([[:digit:]]{1,2})([[:alpha:]]{2})/, "\\1<sup>\\2</sup>",
        "1");
        print;
    }

I use `<h4>` headlines for the date indications, which are indicated as
`####` in `markdown`. The part to be put inside of `<sup></sup>` only
consists of the two letters following a one- or two-digit number, it is
captured in the second group and printed using the second back references
(`\\2`). Since only one occurrence has to be replaced--there _is_ only one
possible occurrence--the quantity `1` rather than the flag `g` has been used.

Now we run into the problem that the line containing the date indication
is processed twice: Firstly, by the function matching for pound characters;
and, secondly, by the function matching anything _not_ starting with white
space. So we have to exclude the date indications from the other function:

    !/^([[:space:]]+|####)/ {
        # typographical replacements
    }

Now that function matches any line _not_ starting with one or many spaces
or four pound characters.

Here's the `typography.awk` script in full:

    /^[[:space:]]+/ {
        print;
    }

    !/^([[:space:]]+|####)/ {
        sub(/^"/, "“");
        $0 = gensub(/([[:space:]]+)"/, "\\1“", "g");
        gsub(/"/, "”");
        sub(/^'/, "‘");
        $0 = gensub(/([[:space:]]+)'/, "\\1‘", "g");
        gsub(/'/, "’");
        sub(/\.\.\./, "…");
        sub(/--/, "‒");
        print;
    }

    /^####/ {
        $0 = gensub(/([[:digit:]]{1,2})([[:alpha:]]{2})/, "\\1<sup>\\2</sup>",
        "1");
        print;
    }

The `generate.sh` script also needed some modifications: The typographic
operations are now performed _before_ `markdown` converted the article to HTML:

    #!/bin/bash

    PAGE=index.html

    cat html/header.html > $PAGE
    ls articles/*.md | sort -r | while read md_file; do
        echo '<article>' >> $PAGE
        awk -f 'awk/typography.awk' $md_file | markdown \
            | sed '/^\s*$/d' | sed -f 'sed/add_link.sed' >> $PAGE
        echo '</article>' >> $PAGE
    done
    cat html/footer.html >> $PAGE

Maybe the `add_link.sed` script would be easier in `awk`, too? At least I'll
be coding again when trying!
