#### Wednesday, December 7th 2016

## 008: RSS Feed

As my friend [meillo](http://marmaro.de) suggested, I introduced an [RSS
feed](https://patrickbucher.github.io/starting-over/feed.rss). I wasn't
very motivated to do that in the first place, because RSS is XML, after
all. But, nonetheless, I did the first step and implemented [links to
individual articles](#005) a couple of days ago, so that I could use them
in the feed. First, I thought that Python (and some XML or RSS library)
rather than `awk` would be the proper tool for the task. But then I thought
how it could be done using `awk`, and I did so in combination with `bash`.

First, let's have look at the big picture, the `rss.sh` script, that is:

    #!/bin/bash
    rm -f feed.rss
    awk -F '=' -f awk/rss-header.awk meta.txt > feed.rss
    ls articles/*.md | sort -r | while read md_file; do
        awk -f awk/rss-body.awk $md_file >> feed.rss
    done
    echo -e "</channel>\n</rss>" >> feed.rss

This script creates (but first deletes) a file called `feed.rss`, the actual
RSS feed. It does so in three steps:

1. It creates the header based on the file `meta.txt`, then
2. it iterates over all articles and creates an item for each of them,
3. and it adds a footer using the `echo` command.

The file `meta.txt` contains metadata and looks as follows:

    title=Starting Over
    url=https://patrickbucher.github.io/starting-over/
    description=Let’s make some progress again.
    language=en
    author=Patrick Bucher, patrick.bucher87@gmail.com

These are simple key-value pairs, separated by an equals sign. The
`rss-header.awk` script is called, setting the `-F` parameter to the equals
sign, so that the key goes into variable `$1` and the value into variable
`$2`. Here's the full script `rss-header.awk`:

    BEGIN {
        print "<?xml version=\"1.0\" encoding=\"utf-8\"?>"
        print "<rss version=\"2.0\"
        xmlns:atom=\"http://www.w3.org/2005/Atom\">"
        print "<channel>"
        printf "<atom:link
        href=\"https://patrickbucher.github.io/starting-over/feed.rss\" "
        print "rel=\"self\" type=\"application/rss+xml\" />"
    }
    /^title=/ {
        print "<title>" $2 "</title>"
    }
    /^url=/ {
        print "<link>" $2 "</link>"
    }
    /^description=/ {
        print "<description>" $2 "</description>"
    }
    /^language=/ {
        print "<language>" $2 "</language>"
    }
    /^author=/ {
        print "<copyright>" $2 "</copyright>"
    }
    END {
        print "<pubDate>" strftime("%a, %d %b %Y %H:%M:%S %z") "</pubDate>"
    }

At the beginning, the RSS header (with an optional but recommended
atom link) is created. Then, for every match of a meta variable (title,
url, description, language and author) a header tag is created (title,
link, description, language and copyright). Furthermore, the current
date and time is added as the pubDate tag using [RFC-822 date-time
format](http://www.feedvalidator.org/docs/error/InvalidRFC2822Date.html).

The `rss-body.awk` script is invoked once for every article. It generates one
`<item>` for every article, which is opened in `BEGIN` and closed in `END`:

    BEGIN {
        print "<item>"
    }
    END {
        print "</item>"
    }

The main title is matched with two leading number signs (`##`), which are cut
off for the `<title>` tag using the `gensub()` function. For both `<url>`
and `<guid>` the URL to the individual article is used. The article number
has to be extracted for that purpose, which is done using the `substr()`
function. Here's the whole function:

    /^## / {
        $0 = gensub(/^## (.+)/, "\\1", "1");
        print "<title>" $0 "</title>"

        $1 = substr($1, 0, 3); # 001: -> 001
        url = "https://patrickbucher.github.io/starting-over/#" $1
        print "<link>" url "</link>"

        print "<guid>" url "</guid>"
    }

Just like the `<pubDate>` field in the header, the `<pubDate>` field for
individual also requires the RFC-822 format. Unfortunately, I write the
publication dates like this: "Wednesday, December 7th 2016". So this date
string needs to be converted accordingly. Since I don't indicate a time, I
have to make it up and just picked midnight, so that the channel's `pubDate`
will always be later than every individual article's `pubDate`.

Weekday and month are easy to handle, I just have to cut off the first three
characters with `substr()`. The month's day can easily be matched with one
or more digits, followed by two letters using `gensub()`. As mentioned,
the time is made up using CET, which is GMT +1 hour. Here's the full function:

    /^#### / {
        weekday = $2
        month = $3
        monthday = $4
        year = $5

        printf "<pubDate>"
        printf substr(weekday, 0, 3) ", " # Monday, -> Mon,
        printf gensub(/([[:digit:]]+)[[:alpha:]]{2}/, "\\1", "1", monthday)
        " " # 4th -> 4
        printf substr(month, 0, 3) " " # January -> Jan
        printf year " "
        printf "00:00:00 +0100"
        print "</pubDate>"
    }

The `<description>` has to be wrapped in a `CDATA` section. Since I don't
want to put the whole article inside it, but just the first paragraph, I call
`exit` to stop processing matching lines--those starting with one or many
alphanumeric characters:

    /^[[:alpha:]]+/ {
        print "<description><![CDATA[" $0 " …]]></description>"
        exit
    }

Of course I also have to call the `rss.sh` script from `generate.sh`, so
that the Website and the RSS feed always represent the same content.
